# oracle wm_concat替换方案

> 对于部分版本中，返回字段类型为varchar2的数据库,解决当拼接的字段大于定义的变量长度时，该函数报错的问题.

```sql
CREATE OR REPLACE TYPE wm_concat_new_im
AUTHID CURRENT_USER AS OBJECT
(
  CURR_STR clob,
  STATIC FUNCTION ODCIAGGREGATEINITIALIZE(SCTX IN OUT wm_concat_new_im) RETURN NUMBER,
  MEMBER FUNCTION ODCIAGGREGATEITERATE(SELF IN OUT wm_concat_new_im,
  P1 IN VARCHAR2) RETURN NUMBER,
  MEMBER FUNCTION ODCIAGGREGATETERMINATE(SELF IN wm_concat_new_im,
  RETURNVALUE OUT clob,
  FLAGS IN NUMBER)
  RETURN NUMBER,
  MEMBER FUNCTION ODCIAGGREGATEMERGE(SELF IN OUT wm_concat_new_im,
  SCTX2 IN wm_concat_new_im) RETURN NUMBER
);

CREATE OR REPLACE TYPE BODY wm_concat_new_im
IS
  STATIC FUNCTION ODCIAGGREGATEINITIALIZE(SCTX IN OUT wm_concat_new_im)
  RETURN NUMBER
  IS
  BEGIN
  SCTX := wm_concat_new_im(NULL);
  RETURN ODCICONST.SUCCESS;
  END;
  MEMBER FUNCTION ODCIAGGREGATEITERATE(SELF IN OUT wm_concat_new_im,P1 IN VARCHAR2)
  RETURN NUMBER
  IS
  BEGIN
  IF(CURR_STR IS NOT NULL) THEN
  CURR_STR := CURR_STR || ',' || P1;
  ELSE
  CURR_STR := P1;
  END IF;
  RETURN ODCICONST.SUCCESS;
  END;
  MEMBER FUNCTION ODCIAGGREGATETERMINATE(SELF IN wm_concat_new_im,RETURNVALUE OUT clob,FLAGS IN NUMBER)
  RETURN NUMBER
  IS
  BEGIN
  RETURNVALUE := CURR_STR;
  RETURN ODCICONST.SUCCESS;
  END;
  MEMBER FUNCTION ODCIAGGREGATEMERGE(SELF IN OUT wm_concat_new_im,SCTX2 IN wm_concat_new_im)
  RETURN NUMBER
  IS
  BEGIN
  IF(SCTX2.CURR_STR IS NOT NULL) THEN
  SELF.CURR_STR := SELF.CURR_STR || ',' || SCTX2.CURR_STR ;
  END IF;
  RETURN ODCICONST.SUCCESS;
  END;
END;

create or replace FUNCTION wm_concat_new(P1 VARCHAR2)
RETURN clob AGGREGATE USING wm_concat_new_im ;
```